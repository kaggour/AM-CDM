uri "http://kdl.ge.com/AM-CDM-base" alias AM-CDM-base version "$Revision:$ Last modified on   $Date:$".

Document (note "File of any type.") is a class,
	described by documentTitle (note "Title of the document.") with a single value of type string,
	described by documentFileName (note "File name of the document.") with a single value of type string,
	described by documentAuthor (note "One or more authors of the document.") with values of type Person,
	described by documentLocation (note "Physical location of the document.") with a single value of type string,
	described by documentFormat (note "Defines the file structure, not the file type.") with a single value of type string,
	described by documentClassificationLevel (note "Classification level of the document.") with a single value of type string,
	described by documentClassificationIPClass (note "IP class classification of the document.") with a single value of type string,
	described by documentURI (note "URI of the document pointing to its digital location.") with a single value of type string,
	described by documentCreationTime (note "Time the document is created.") with a single value of type string.

Organization (note "An organization of various types.") is a class,
	described by organizationID (note "The unique identifier of the organization.") with a single value of type string,
	described by organizationName (note "The name of an organization.") with a single value of type string,
	described by organizationType (note "The type or roles of an organization, whether it is a manufacturer, vendor, supplier, or contractor. One organization may have many types.") with values of type OrganizationType,
	described by organizationQualification (note "Details of the organization's qualifications and certifications.") with values of type OrganizationQualification,
	described by organizationAddress (note "The address of the organization.") with a single value of type Address,
	described by organizationPhoneNumber (note "A phone number for the organization.") with a single value of type PhoneNumber,
	described by organizationPointOfContact (note "Identification of the point of contact. You may wish to reference by ID to avoid possible circular references.") with a single value of type Person.

OrganizationType (note "An OrganizationType is a recognized capability of an Organization.") is a class, must be one of {MaterialManufacturer, MaterialSupplier, PartDesigner, PartManufacturer, MachineManufacturer, SystemIntegrator, PartTester, Regulator, Buyer, FundingAgency, Other}.

Person (note "An Individual and their primary credentials and contact information.") is a class,
	described by personID (note "Unique identifier of the person.") with a single value of type string,
	described by personFirstName (note "First name of person.") with a single value of type string,
	described by personLastName (note "Last name of person.") with a single value of type string,
	described by personFullName (note "Full name of person if not split into first and last names.") with a single value of type string,
	//described by personRole (note "Role of individual performing task.") with a single value of type string,
	described by personEmail (note "The email for the individual.") with a single value of type string,
	described by personPhoneNumber (note "The phone number for the individual.") with values of type PhoneNumber,
	described by personQualification (note "Qualifications of person.") with values of type PersonQualification,
	described by personOrganization (note "Links person to an organization. You may wish to reference by ID to avoid possible circular references.") with a single value of type Organization.

OrganizationQualification (note "Details of qualification/certification for an individual or organization.") is a type of Qualification.

PersonQualification (note "Details of qualification/certification for a person.") is a type of Qualification,
	described by personQualificationNumberOfHours (note "The number of hours that this person has completed on this equipment/software/system.") with a single value of type int.

Qualification (note "Details of qualification/certification (machine specific operator certificate, UL, ASTM).") is a class,
	described by qualificationType (note "The type of qualification/certification.") with a single value of type string,
	described by qualificationLevel (note "The level of qualification/certification.") with a single value of type string,
	described by qualifyingOrganization (note "The organization from which the qualification or certification originates. You may wish to reference by ID to avoid possible circular references.") with a single value of type Organization.

Address (note "A physical address where someone or something is located.") is a class,
	described by addressLine1 (note "First line in address.") with a single value of type string,
	described by addressLine2 (note "Second line in address.") with a single value of type string,
	described by addressCity (note "City in address.") with a single value of type string,
	described by addressStateProvince (note "State or Province in address.") with a single value of type string,
	described by adderssPostalCode (note "Zip or postal code in address.") with a single value of type string,
	described by addressCountry (note "Country in address.") with a single value of type Country.

PhoneNumber (note "A phone number.") is a class,
	described by phoneCountry (note "Country phone number is in.") with a single value of type Country,
	described by phoneNumber (note "Actual phone number.") with a single value of type string.

Country (note "A country.") is a class,
	described by countryName (note "The country's name.") with a single value of type string,
	described by countryAlpha2Code (note "Two-digit country code.") with a single value of type string,
	described by countryAlpha3Code (note "Three-digit country code.") with a single value of type string,
	described by countryCallingCode (note "The calling code for the country.") with a single value of type string.

Environment is a class,
	described by environmentAmbientTemperature with a single value of type Quantity,
	described by environmentAmbientPressure with a single value of type Quantity.

Coordinate (note "A coordinate to position an object in reference to something.") is a class.

CartesianCoordinate (note "A Cartesian coordinate.") is a type of Coordinate,
	described by xCoordinate (note "The position on the x-axis in a Cartesian coordinate system.") with a single value of type Quantity,
	described by yCoordinate (note "The position on the y-axis in a Cartesian coordinate system.") with a single value of type Quantity,
	described by zCoordinate (note "The position on the z-axis in a Cartesian coordinate system.") with a single value of type Quantity.

RotaryCoordinate (note "A rotary coordinate.") is a type of Coordinate,
	described by aAngle (note "The rotation of the A angle in a rotary coordinate system.") with a single value of type Quantity,
	described by bAngle (note "The rotation of the B angle in a rotary coordinate system.") with a single value of type Quantity,
	described by cAngle (note "The rotation of the C angle in a rotary coordinate system.") with a single value of type Quantity.

InitialOrientation is a class, must be one of {XYZ, XY, XZY, XZ, YXZ, YX, YZX, YZ, ZXY, ZX, ZYX, ZY, X, Y, Z}.

Orientation (note "The orientation of an object as defined in ISO/ASTM 52921.") is a class,
	described by partLocationX (note "The x-coordinate position of the geometric center of the design's arbitrarily oriented minimum bounding box with respect to the system origin. ISO/ASTM 52921") with a single value of type Quantity,
	described by partLocationY (note "The y-coordinate position of the geometric center of the design's arbitrarily oriented minimum bounding box with respect to the system origin. ISO/ASTM 52921") with a single value of type Quantity,
	described by partLocationZ (note "The z-coordinate position of the geometric center of the design's arbitrarily oriented minimum bounding box with respect to the system origin. ISO/ASTM 52921") with a single value of type Quantity,
	described by partReorientationA (note "Specification of the rotation of the design's arbitrarily oriented minimum bounding box around the geometric center in the sequence A (rotation about x), B (rotation about y), then C (rotation about z). ISO/ASTM 52921.") with a single value of type Quantity,
	described by partReorientationB (note "Specification of the rotation of the design's arbitrarily oriented minimum bounding box around the geometric center in the sequence A (rotation about x), B (rotation about y), then C (rotation about z). ISO/ASTM 52921.") with a single value of type Quantity,
	described by partReorientationC (note "Specification of the rotation of the design's arbitrarily oriented minimum bounding box around the geometric center in the sequence A (rotation about x), B (rotation about y), then C (rotation about z). ISO/ASTM 52921.") with a single value of type Quantity.

Quantity (note "Abstract class representing a numerical quantity with a unit.  This class should not be used directly, rather one should use either the SingleValue or StatisticalValue classes.") is a class,
	described by quantityUnit (note "Units associated with the numerical value.") with a single value of type string.

SingleValue (note "A single quantity value with a value, a single measure of uncertainty, and a unit.") is a type of Quantity,
	described by valueValue (note "Numerical value.") with a single value of type double,
	described by valueUncertaintyMeasure (note "") with a single value of type double.

StatisticalValue (note "A statistical quantity value with a variety of statistical measures.") is a type of Quantity,
	described by statMeanValue (note "Mean (average) value of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statMedianValue (note "Median value of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statModeValue (note "Mode of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statStandardDeviationValue (note "Standard deviation of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statMinValue (note "Minimum value of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statMaxValue (note "Maximum value of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statNumberOfValues (note "Number (count) of the multiple values, if the numerical value was derived multiple values.") with a single value of type double,
	described by statDataAggregationTechnique (note "Data aggregation technique, if the numerical value was derived multiple values.") with a single value of type string,
	described by statAggregatedValues (note "") with values of type SingleValue.

DatasetMetadata (note "Metadata associated with a dataset.") is a class,
	described by datasetName (note "Name of the dataset.") with a single value of type string,
	described by datasetID (note "Unique identifier of the dataset.") with a single value of type string,
	described by datasetSubject (note "Classification, keyword, or phrase that describes the topic of the dataset at the endpoint of the URI. This is a refinement of the Dublin Core definition that states this is the topic of the resource and the plurality of keywords, etc. It should be noted that the recommended best practice is to use a controlled vocabulary (per Dublin Core).") with values of type string,
	described by datasetType (note "Dataset type, i.e., raw, AI-generated, or synthesized data.") with a single value of type string,
	described by datasetURI (note "URI location where a dataset can be found.") with a single value of type string,
	described by datasetFormat (note "Formal description language that describes the format or layout of the file, such as CSV with Headers, Protobuf, fixed position, JSON Schema, XML, Cobol Copybook, X12, HDF5, ZIP, and so forth.") with values of type string,
	described by datasetStructure (note "Detailed text description of the format of the data, e.g., the XML file format, JSON file format, HDf5 format, etc.") with a single value of type string,
	described by datasetSchemaURI (note "Location via a URI pointer to the specific Content Format Specification that describes how to parse the content in a repository; i.e., defines the schema of the content in the repository.") with a single value of type string,
	described by datasetCreator (note "Creator of dataset and/or the metadata of the dataset.") with values of type Person,
	described by datasetCreationDateTime (note "Point of time associated with creation of the dataset. May be used at any level of granularity. Recommended best practice is to use an encoding scheme, such as the W3CDTF profile of ISO 8601 [W3CDTF].") with a single value of type dateTime,
	described by datasetSize (note "The size of the dataset.") with a single value of type Quantity,
	described by datasetQuantityType (note "Type of Measurement(s) that would appear in a dataset; e.g., pH, Moisture, Temperature, requiring an industry group to define the enumerated list/controlled vocabulary for the specific business context.") with values of type string,
	described by datasetKeyTags (note "Keywords and tags describing the data to facilitate search and findability. Examples include 'meltpool temperature', 'meltpool size', and 'layer-wise image.'") with values of type string,
	described by datasetAccessControl (note "How to get access to the dataset. Control Code Type is used to communicate 'Restricted', 'Consent', and 'Public'.") with a single value of type DatasetAccessControl.

DatasetAccessControl (note "Possible types of data access control.") is a class, must be one of {Restricted, Consent, Public}.

Project (note "A temporary endeavor undertaken to create one or more unique AM products or AM services. Links to all other information; may include platform, criticality, sponsors, material DB ID, organization, distribution control (e.g., ITAR restricted or not).") is a class,
	described by projectID (note "Reference to the associated Project that produced this build. Links to Project record.") with a single value of type string,
	described by projectCreator (note "Organization that created the project.") with a single value of type Organization,
	described by projectSponsor (note "Provider of money and requirements for the project.") with a single value of type Organization,
	described by projectOwningOrganization (note "Organization that owns the project.") with a single value of type Organization,
	described by projectDistributionStatement (note "Documentation of any restrictions of access or distribution of the data collected and analyzed under this project, by organizations other than the authoritative source.") with a single value of type string,
	described by projectRequirements (note "Documented requirements for the project.") with a single value of type Document,
	described by projectPlan (note "Document describing how project is to be conducted, organized, etc.") with a single value of type Document,
	described by projectStart (note "Start date of project.") with a single value of type dateTime,
	described by projectEnd (note "End date of project.") with a single value of type dateTime,
	described by projectStandards (note "Set of standards used in project.") with values of type string,
	described by projectQualityControlPlan (note "Document describing the quality control plan for the the site performing the project.") with a single value of type Document,
	described by projectQualityProcessControlPlan (note "Document describing the quality process control plan for the the site performing the project.") with a single value of type Document,
	described by projectDeliveryDate (note "Requested delivery date of project.") with a single value of type dateTime,
	described by projectDistributionControl (note "ITAR-like restrictions.") with a single value of type string,
	described by projectDataset (note "Datasets generated from the project.") with values of type DatasetMetadata,
	described by projectReport (note "Reports of the project.") with values of type Document,
	described by projectDataManagementPlan (note "Data management plan of the project.") with a single value of type Document.

SynthesizedData (note "Data that has been reduced from its original raw form and often indicates clearer, more human-friendly, interpretable results. This could include; statistical or trend related results, material cards (for design/simulation), or similar.") is a type of DatasetMetadata,
	described by synthesizedDataMethod (note "The method used to synthesize the data.") with values of type string,
	described by synthesizedDocument with a single value of type Document.
